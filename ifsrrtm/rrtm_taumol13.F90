!----------------------------------------------------------------------------
SUBROUTINE RRTM_TAUMOL13 (KIDIA,KFDIA,KLEV,P_TAU,&
 & P_TAUAERL,P_FAC00,P_FAC01,P_FAC10,P_FAC11,P_FORFAC,P_FORFRAC,K_INDFOR,K_JP,K_JT,K_JT1,P_ONEMINUS,&
 & P_COLH2O,P_COLN2O,P_COLCO2,P_COLO3,P_COLDRY,K_LAYTROP,P_SELFFAC,P_SELFFRAC,K_INDSELF,PFRAC, &
 & PRAT_H2ON2O, PRAT_H2ON2O_1,PMINORFRAC,KINDMINOR)  

!     BAND 13:  2080-2250 cm-1 (low - H2O,N2O; high - nothing)

!     AUTHOR.
!     -------
!      JJMorcrette, ECMWF

  !     MODIFICATIONS.
!     --------------
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      NEC           25-Oct-2007 Optimisations
!      JJMorcrette 20110613 flexible number of g-points
!      ABozzo 201306 updated to rrtmg v4.85
!     band 13:  2080-2250 cm-1 (low key - h2o,n2o; high minor - o3 minor)
! ---------------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK, DR_HOOK, JPHOOK

USE PARRRTM  , ONLY : JPBAND
USE YOERRTM  , ONLY : JPGPT  ,NG13  ,NGS12
USE YOERRTWN , ONLY : NSPA   
USE YOERRTA13, ONLY : ABSA   ,FRACREFA,FRACREFB,SELFREF,FORREF,KA_MCO2, KA_MCO, KB_MO3
USE YOERRTRF, ONLY : CHI_MLS

IMPLICIT NONE

INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: P_TAU(KIDIA:KFDIA,JPGPT,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_TAUAERL(KIDIA:KFDIA,KLEV,JPBAND) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_FAC00(KIDIA:KFDIA,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_FAC01(KIDIA:KFDIA,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_FAC10(KIDIA:KFDIA,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_FAC11(KIDIA:KFDIA,KLEV) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_JP(KIDIA:KFDIA,KLEV) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_JT(KIDIA:KFDIA,KLEV) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_JT1(KIDIA:KFDIA,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_ONEMINUS
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_COLH2O(KIDIA:KFDIA,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_COLN2O(KIDIA:KFDIA,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_COLCO2(KIDIA:KFDIA,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_COLO3(KIDIA:KFDIA,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_COLDRY(KIDIA:KFDIA,KLEV) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_LAYTROP(KIDIA:KFDIA) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_SELFFAC(KIDIA:KFDIA,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_SELFFRAC(KIDIA:KFDIA,KLEV) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_INDSELF(KIDIA:KFDIA,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFRAC(KIDIA:KFDIA,JPGPT,KLEV) 

REAL(KIND=JPRB)   ,INTENT(IN)   :: PRAT_H2ON2O(KIDIA:KFDIA,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)   :: PRAT_H2ON2O_1(KIDIA:KFDIA,KLEV)
INTEGER(KIND=JPIM),INTENT(IN)   :: K_INDFOR(KIDIA:KFDIA,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)   :: P_FORFAC(KIDIA:KFDIA,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)   :: P_FORFRAC(KIDIA:KFDIA,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)   :: PMINORFRAC(KIDIA:KFDIA,KLEV)
INTEGER(KIND=JPIM),INTENT(IN)   :: KINDMINOR(KIDIA:KFDIA,KLEV)

! ---------------------------------------------------------------------------


REAL(KIND=JPRB) :: Z_SPECCOMB(KLEV),Z_SPECCOMB1(KLEV), Z_SPECCOMB_PLANCK(KLEV), &
                   & Z_SPECCOMB_MCO2(KLEV), Z_SPECCOMB_MCO(KLEV)
INTEGER(KIND=JPIM) :: IND0(KLEV),IND1(KLEV),INDS(KLEV),INDF(KLEV),INDM(KLEV)
INTEGER(KIND=JPIM) :: IG, JS, JLAY, JS1, JPL, JMCO2, JMCO
INTEGER(KIND=JPIM) :: JLON

REAL(KIND=JPRB) :: ZREFRAT_PLANCK_A, ZREFRAT_M_A, ZREFRAT_M_A3
REAL(KIND=JPRB) ::  Z_FAC000, Z_FAC100, Z_FAC200,&
 & Z_FAC010, Z_FAC110, Z_FAC210, &
 & Z_FAC001, Z_FAC101, Z_FAC201, &
 & Z_FAC011, Z_FAC111, Z_FAC211
REAL(KIND=JPRB) :: ZP, ZP4, ZFK0, ZFK1, ZFK2

REAL(KIND=JPRB) :: ZTAUFOR,ZTAUSELF,ZTAU_MAJOR,ZTAU_MAJOR1, ZCO2M1, ZCO2M2, ZABSCO2
REAL(KIND=JPRB) :: ZCOM1, ZCOM2, ZABSCO, ZABSO3
REAL(KIND=JPRB) :: ZCHI_CO2, ZRATCO2, ZADJFAC, ZADJCOLCO2(KIDIA:KFDIA,KLEV)

REAL(KIND=JPRB) :: Z_FS, Z_SPECMULT, Z_SPECPARM,  &
& Z_FS1, Z_SPECMULT1, Z_SPECPARM1, &
& Z_FMCO2, Z_SPECMULT_MCO2, Z_SPECPARM_MCO2, &
& Z_FMCO , Z_SPECMULT_MCO , Z_SPECPARM_MCO , &
& Z_FPL, Z_SPECMULT_PLANCK, Z_SPECPARM_PLANCK

REAL(KIND=JPRB)   :: Z_COLCO(KIDIA:KFDIA,KLEV) !left =0 for now,not passed from rrtm_gasbas1a

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

 
ASSOCIATE(NFLEVG=>KLEV)
IF (LHOOK) CALL DR_HOOK('RRTM_TAUMOL13',0,ZHOOK_HANDLE)

! Minor gas mapping levels :
!     lower - co2, p = 1053.63 mb, t = 294.2 k
!     lower - co, p = 706 mb, t = 278.94 k
!     upper - o3, p = 95.5835 mb, t = 215.7 k


! Calculate reference ratio to be used in calculation of Planck
! fraction in lower/upper atmosphere.

! P = 473.420 mb (Level 5)
      ZREFRAT_PLANCK_A = CHI_MLS(1,5)/CHI_MLS(4,5)

! P = 1053. (Level 1)
      ZREFRAT_M_A = CHI_MLS(1,1)/CHI_MLS(4,1)

! P = 706. (Level 3)
      ZREFRAT_M_A3 = CHI_MLS(1,3)/CHI_MLS(4,3)

! Compute the optical depth by interpolating in ln(pressure), 
! temperature, and appropriate species.  Below laytrop, the water
! vapor self-continuum and foreign continuum is interpolated 
! (in temperature) separately.  


DO JLAY = 1, KLEV
  DO JLON = KIDIA, KFDIA
    Z_COLCO(JLON,JLAY) = 0.0_JPRB
    IF (JLAY <= K_LAYTROP(JLON)) THEN
      Z_SPECCOMB(JLAY) = P_COLH2O(JLON,JLAY) + PRAT_H2ON2O(JLON,JLAY)*P_COLN2O(JLON,JLAY)
      Z_SPECPARM = P_COLH2O(JLON,JLAY)/Z_SPECCOMB(JLAY)
      Z_SPECPARM=MIN(P_ONEMINUS,Z_SPECPARM)
      Z_SPECMULT = 8._JPRB*(Z_SPECPARM)
      JS = 1 + INT(Z_SPECMULT)
      Z_FS = MOD(Z_SPECMULT,1.0_JPRB)

      Z_SPECCOMB1(JLAY) = P_COLH2O(JLON,JLAY) + PRAT_H2ON2O_1(JLON,JLAY)*P_COLN2O(JLON,JLAY)
      Z_SPECPARM1 = P_COLH2O(JLON,JLAY)/Z_SPECCOMB1(JLAY)
      IF (Z_SPECPARM1 >= P_ONEMINUS) Z_SPECPARM1 = P_ONEMINUS
      Z_SPECMULT1 = 8._JPRB*(Z_SPECPARM1)
      JS1 = 1 + INT(Z_SPECMULT1)
      Z_FS1 = MOD(Z_SPECMULT1,1.0_JPRB)

      Z_SPECCOMB_MCO2(JLAY) = P_COLH2O(JLON,JLAY) + ZREFRAT_M_A*P_COLN2O(JLON,JLAY)
      Z_SPECPARM_MCO2 = P_COLH2O(JLON,JLAY)/Z_SPECCOMB_MCO2(JLAY)
      IF (Z_SPECPARM_MCO2 >= P_ONEMINUS) Z_SPECPARM_MCO2 = P_ONEMINUS
      Z_SPECMULT_MCO2 = 8._JPRB*Z_SPECPARM_MCO2
      JMCO2 = 1 + INT(Z_SPECMULT_MCO2)
      Z_FMCO2 = MOD(Z_SPECMULT_MCO2,1.0_JPRB)
! In atmospheres where the amount of CO2 is too great to be considered
! a minor species, adjust the column amount of CO2 by an empirical factor 
! to obtain the proper contribution.
      ZCHI_CO2 = P_COLCO2(JLON,JLAY)/P_COLDRY(JLON,JLAY)
      ZRATCO2 = 1.E20_JPRB*ZCHI_CO2/3.55E-4_JPRB
      IF (ZRATCO2 > 3.0_JPRB) THEN
         ZADJFAC = 2.0_JPRB+(ZRATCO2-2.0_JPRB)**0.68_JPRB
         ZADJCOLCO2(JLON,JLAY) = ZADJFAC*3.55E-4*P_COLDRY(JLON,JLAY)*1.E-20_JPRB
      ELSE
         ZADJCOLCO2(JLON,JLAY) = P_COLCO2(JLON,JLAY)
      ENDIF

      Z_SPECCOMB_MCO(JLAY) = P_COLH2O(JLON,JLAY) + ZREFRAT_M_A3*P_COLN2O(JLON,JLAY)
      Z_SPECPARM_MCO = P_COLH2O(JLON,JLAY)/Z_SPECCOMB_MCO(JLAY)
      IF (Z_SPECPARM_MCO >= P_ONEMINUS) Z_SPECPARM_MCO = P_ONEMINUS
      Z_SPECMULT_MCO = 8._JPRB*Z_SPECPARM_MCO
      JMCO = 1 + INT(Z_SPECMULT_MCO)
      Z_FMCO = MOD(Z_SPECMULT_MCO,1.0_JPRB)

      Z_SPECCOMB_PLANCK(JLAY) = P_COLH2O(JLON,JLAY)+ZREFRAT_PLANCK_A*P_COLN2O(JLON,JLAY)
      Z_SPECPARM_PLANCK = P_COLH2O(JLON,JLAY)/Z_SPECCOMB_PLANCK(JLAY)
      IF (Z_SPECPARM_PLANCK >= P_ONEMINUS) Z_SPECPARM_PLANCK=P_ONEMINUS
      Z_SPECMULT_PLANCK = 8._JPRB*Z_SPECPARM_PLANCK
      JPL= 1 + INT(Z_SPECMULT_PLANCK)
      Z_FPL = MOD(Z_SPECMULT_PLANCK,1.0_JPRB)

      IND0(JLAY) = ((K_JP(JLON,JLAY)-1)*5+(K_JT(JLON,JLAY)-1))*NSPA(13) + JS
      IND1(JLAY) = (K_JP(JLON,JLAY)*5+(K_JT1(JLON,JLAY)-1))*NSPA(13) + JS1
      INDS(JLAY) = K_INDSELF(JLON,JLAY)
      INDF(JLAY) = K_INDFOR(JLON,JLAY)
      INDM(JLAY) = KINDMINOR(JLON,JLAY)
  
IF (Z_SPECPARM < 0.125_JPRB) THEN
            ZP = Z_FS - 1
            ZP4 = ZP**4
            ZFK0 = ZP4
            ZFK1 = 1 - ZP - 2.0_JPRB*ZP4
            ZFK2 = ZP + ZP4
            Z_FAC000 = ZFK0*P_FAC00(JLON,JLAY)
            Z_FAC100 = ZFK1*P_FAC00(JLON,JLAY)
            Z_FAC200 = ZFK2*P_FAC00(JLON,JLAY)
            Z_FAC010 = ZFK0*P_FAC10(JLON,JLAY)
            Z_FAC110 = ZFK1*P_FAC10(JLON,JLAY)
            Z_FAC210 = ZFK2*P_FAC10(JLON,JLAY)
      ELSEIF (Z_SPECPARM > 0.875_JPRB) THEN
            ZP = -Z_FS 
            ZP4 = ZP**4
            ZFK0 = ZP4
            ZFK1 = 1 - ZP - 2.0_JPRB*ZP4
            ZFK2 = ZP + ZP4
            Z_FAC000 = ZFK0*P_FAC00(JLON,JLAY)
            Z_FAC100 = ZFK1*P_FAC00(JLON,JLAY)
            Z_FAC200 = ZFK2*P_FAC00(JLON,JLAY)
            Z_FAC010 = ZFK0*P_FAC10(JLON,JLAY)
            Z_FAC110 = ZFK1*P_FAC10(JLON,JLAY)
            Z_FAC210 = ZFK2*P_FAC10(JLON,JLAY)
      ELSE
            Z_FAC000 = (1._JPRB - Z_FS) * P_FAC00(JLON,JLAY)
            Z_FAC010 = (1._JPRB - Z_FS) * P_FAC10(JLON,JLAY)
            Z_FAC100 = Z_FS * P_FAC00(JLON,JLAY)
            Z_FAC110 = Z_FS * P_FAC10(JLON,JLAY)
      ENDIF
      IF (Z_SPECPARM1 < 0.125_JPRB) THEN
            ZP = Z_FS1 - 1
            ZP4 = ZP**4
            ZFK0 = ZP4
            ZFK1 = 1 - ZP - 2.0_JPRB*ZP4
            ZFK2 = ZP + ZP4
            Z_FAC001 = ZFK0*P_FAC01(JLON,JLAY)
            Z_FAC101 = ZFK1*P_FAC01(JLON,JLAY)
            Z_FAC201 = ZFK2*P_FAC01(JLON,JLAY)
            Z_FAC011 = ZFK0*P_FAC11(JLON,JLAY)
            Z_FAC111 = ZFK1*P_FAC11(JLON,JLAY)
            Z_FAC211 = ZFK2*P_FAC11(JLON,JLAY)
      ELSEIF (Z_SPECPARM1 > 0.875_JPRB) THEN
            ZP = -Z_FS1 
            ZP4 = ZP**4
            ZFK0 = ZP4
            ZFK1 = 1 - ZP - 2.0_JPRB*ZP4
            ZFK2 = ZP + ZP4
            Z_FAC001 = ZFK0*P_FAC01(JLON,JLAY)
            Z_FAC101 = ZFK1*P_FAC01(JLON,JLAY)
            Z_FAC201 = ZFK2*P_FAC01(JLON,JLAY)
            Z_FAC011 = ZFK0*P_FAC11(JLON,JLAY)
            Z_FAC111 = ZFK1*P_FAC11(JLON,JLAY)
            Z_FAC211 = ZFK2*P_FAC11(JLON,JLAY)
      ELSE
            Z_FAC001 = (1._JPRB - Z_FS1) * P_FAC01(JLON,JLAY)
            Z_FAC011 = (1._JPRB - Z_FS1) * P_FAC11(JLON,JLAY)
            Z_FAC101 = Z_FS1 * P_FAC01(JLON,JLAY)
            Z_FAC111 = Z_FS1 * P_FAC11(JLON,JLAY)
      ENDIF


!-- DS_000515
!CDIR UNROLL=NG13
      DO IG = 1, NG13
!-- DS_000515
        ZTAUSELF = P_SELFFAC(JLON,JLAY)* (SELFREF(INDS(JLAY),IG) + P_SELFFRAC(JLON,JLAY) * &
          &       (SELFREF(INDS(JLAY)+1,IG) - SELFREF(INDS(JLAY),IG)))
        ZTAUFOR = P_FORFAC(JLON,JLAY) * (FORREF(INDF(JLAY),IG) + P_FORFRAC(JLON,JLAY) * &
          &       (FORREF(INDF(JLAY)+1,IG) - FORREF(INDF(JLAY),IG))) 
        ZCO2M1 = KA_MCO2(JMCO2,INDM(JLAY),IG) + Z_FMCO2 * &
          &       (KA_MCO2(JMCO2+1,INDM(JLAY),IG) - KA_MCO2(JMCO2,INDM(JLAY),IG))
        ZCO2M2 = KA_MCO2(JMCO2,INDM(JLAY)+1,IG) + Z_FMCO2 * &
          &       (KA_MCO2(JMCO2+1,INDM(JLAY)+1,IG) - KA_MCO2(JMCO2,INDM(JLAY)+1,IG))
        ZABSCO2 = ZCO2M1 + PMINORFRAC(JLON,JLAY) * (ZCO2M2 - ZCO2M1)
        ZCOM1 = KA_MCO(JMCO,INDM(JLAY),IG) + Z_FMCO * &
          &       (KA_MCO(JMCO+1,INDM(JLAY),IG) - KA_MCO(JMCO,INDM(JLAY),IG))
        ZCOM2 = KA_MCO(JMCO,INDM(JLAY)+1,IG) + Z_FMCO * &
          &       (KA_MCO(JMCO+1,INDM(JLAY)+1,IG) - KA_MCO(JMCO,INDM(JLAY)+1,IG))
        ZABSCO = ZCOM1 + PMINORFRAC(JLON,JLAY) * (ZCOM2 - ZCOM1)

      IF (Z_SPECPARM < 0.125_JPRB) THEN
               ZTAU_MAJOR = Z_SPECCOMB(JLAY) * &
                 &   (Z_FAC000 * ABSA(IND0(JLAY),IG) + &
                 &   Z_FAC100 * ABSA(IND0(JLAY)+1,IG) + &
                 &   Z_FAC200 * ABSA(IND0(JLAY)+2,IG) + &
                 &   Z_FAC010 * ABSA(IND0(JLAY)+9,IG) + &
                 &   Z_FAC110 * ABSA(IND0(JLAY)+10,IG) + &
                 &   Z_FAC210 * ABSA(IND0(JLAY)+11,IG))
            ELSEIF (Z_SPECPARM > 0.875_JPRB) THEN
               ZTAU_MAJOR = Z_SPECCOMB(JLAY) * &
                 &   (Z_FAC200 * ABSA(IND0(JLAY)-1,IG) + &
                 &   Z_FAC100 * ABSA(IND0(JLAY),IG) + &
                 &   Z_FAC000 * ABSA(IND0(JLAY)+1,IG) + &
                 &   Z_FAC210 * ABSA(IND0(JLAY)+8,IG) + &
                 &   Z_FAC110 * ABSA(IND0(JLAY)+9,IG) + &
                 &   Z_FAC010 * ABSA(IND0(JLAY)+10,IG))
            ELSE
               ZTAU_MAJOR = Z_SPECCOMB(JLAY) * &
                 &   (Z_FAC000 * ABSA(IND0(JLAY),IG) + &
                 &   Z_FAC100 * ABSA(IND0(JLAY)+1,IG) + &
                 &   Z_FAC010 * ABSA(IND0(JLAY)+9,IG) + &
                 &   Z_FAC110 * ABSA(IND0(JLAY)+10,IG))
            ENDIF

            IF (Z_SPECPARM1 < 0.125_JPRB) THEN
               ZTAU_MAJOR1 = Z_SPECCOMB1(JLAY) * &
                &    (Z_FAC001 * ABSA(IND1(JLAY),IG) + &
                &    Z_FAC101 * ABSA(IND1(JLAY)+1,IG) + &
                &    Z_FAC201 * ABSA(IND1(JLAY)+2,IG) + &
                &    Z_FAC011 * ABSA(IND1(JLAY)+9,IG) + &
                &    Z_FAC111 * ABSA(IND1(JLAY)+10,IG) + &
                &    Z_FAC211 * ABSA(IND1(JLAY)+11,IG))
            ELSEIF (Z_SPECPARM1 > 0.875_JPRB) THEN
               ZTAU_MAJOR1 = Z_SPECCOMB1(JLAY) * &
                &    (Z_FAC201 * ABSA(IND1(JLAY)-1,IG) + &
                &    Z_FAC101 * ABSA(IND1(JLAY),IG) + &
                &    Z_FAC001 * ABSA(IND1(JLAY)+1,IG) + &
                &    Z_FAC211 * ABSA(IND1(JLAY)+8,IG) + &
                &    Z_FAC111 * ABSA(IND1(JLAY)+9,IG) + &
                &    Z_FAC011 * ABSA(IND1(JLAY)+10,IG))
            ELSE
               ZTAU_MAJOR1 = Z_SPECCOMB1(JLAY) * &
                &    (Z_FAC001 * ABSA(IND1(JLAY),IG) +  &
                &    Z_FAC101 * ABSA(IND1(JLAY)+1,IG) + &
                &    Z_FAC011 * ABSA(IND1(JLAY)+9,IG) + &
                &    Z_FAC111 * ABSA(IND1(JLAY)+10,IG))
            ENDIF


        P_TAU(JLON,NGS12+IG,JLAY) = ZTAU_MAJOR + ZTAU_MAJOR1 &
               & + ZTAUSELF + ZTAUFOR &
               & + ZADJCOLCO2(JLON,JLAY)*ZABSCO2 &
               & + Z_COLCO(JLON,JLAY)*ZABSCO &
               & + P_TAUAERL(JLON,JLAY,13)  
        PFRAC(JLON,NGS12+IG,JLAY) = FRACREFA(IG,JPL) + Z_FPL * &
         & (FRACREFA(IG,JPL+1) - FRACREFA(IG,JPL))  
      ENDDO
    ENDIF

!-- JJM_000517
    IF (JLAY > K_LAYTROP(JLON)) THEN
      INDM(JLAY) = KINDMINOR(JLON,JLAY)
!CDIR UNROLL=NG13
      DO IG = 1, NG13
!-- JJM_000517
        ZABSO3 = KB_MO3(INDM(JLAY),IG) + PMINORFRAC(JLON,JLAY) * &
         &       (KB_MO3(INDM(JLAY)+1,IG) - KB_MO3(INDM(JLAY),IG))
        P_TAU(JLON,NGS12+IG,JLAY) = P_COLO3(JLON,JLAY)*ZABSO3+P_TAUAERL(JLON,JLAY,13)
        PFRAC(JLON,NGS12+IG,JLAY) = FRACREFB(IG)
      ENDDO
    ENDIF
  ENDDO
ENDDO

IF (LHOOK) CALL DR_HOOK('RRTM_TAUMOL13',1,ZHOOK_HANDLE)

END ASSOCIATE
END SUBROUTINE RRTM_TAUMOL13
